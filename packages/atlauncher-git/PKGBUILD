# Maintainer: Camas Millar <camas at hotmail dot co dot uk>
# Contributor: Cobalt Space <cobaltspace at protonmail dot com>
# Contributor: Alan Jenkins <alan.james.jenkins at gmail dot com>
# Contributor: Maximilian Berger <snowdragon92 at gmail dot com>

pkgname=atlauncher-git
pkgver=3.2.3.7.r521.g6d2f1d14
pkgrel=2
pkgdesc="A Launcher for Minecraft which integrates multiple different ModPacks to allow you to download and install ModPacks easily and quickly."
arch=(any)
url="https://atlauncher.com/"
license=('GPL3')
depends=('java-runtime=8' 'openal')
makedepends=('git' 'java-environment=8' 'gradle')
provides=('atlauncher')
conflicts=('atlauncher')

source=("git+https://github.com/ATLauncher/ATLauncher.git"
		"atlauncher"
		"atlauncher.desktop"
		"atlauncher.png"
       )

sha256sums=('SKIP'
            '74d6a11ff042fbae373a6ee98f7988489057bf5a7d325270ae5411525982b910'
            '5f45436c96ab9830555d0f987a96fc0b1a9766d450b958aba282820ffca6cc84'
            '369c7aa4439762878fd9970c75d1312cf0cd97119c8320b732addef4a621482d')

pkgver() {
	cd "$srcdir/ATLauncher"
	git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/ATLauncher"
	gradle build
}

package() {
	cd "$srcdir"

	# create folder for the main jar executable
	mkdir -p "$pkgdir/usr/share/java/atlauncher/"
	chmod -R 755 "$pkgdir/usr/share/java/atlauncher/"

	# create folder for other files
	mkdir -p "$pkgdir/usr/share/atlauncher/Downloads"
	chmod 777 "$pkgdir/usr/share/atlauncher/Downloads"

	# install shell wrapper script
	install -D -m755 "$srcdir/atlauncher" "$pkgdir/usr/bin/atlauncher"

	# install jar
	install -D -m644 "$srcdir/ATLauncher/dist/ATLauncher-$(grep -oP "version = '\K[0-9\.]+" $srcdir/ATLauncher/build.gradle).jar" "$pkgdir/usr/share/java/atlauncher/ATLauncher.jar"

	# install desktop launcher with icon
	install -D -m644 "$srcdir/atlauncher.desktop" "$pkgdir/usr/share/applications/atlauncher.desktop"
	install -D -m644 "$srcdir/atlauncher.png" "$pkgdir/usr/share/pixmaps/atlauncher.png"
}
